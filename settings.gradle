pluginManagement {
    repositories {
        //mavenLocal()
        gradlePluginPortal()
        mavenCentral()
        exclusiveContent {
            forRepository {
                maven {
                    name = 'Fabric'
                    url = uri('https://maven.fabricmc.net')
                }
            }
            filter {
                includeGroupAndSubgroups('net.fabricmc')
                includeGroupAndSubgroups('fabric-loom')
            }
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = 'Sponge'
                    url = uri('https://repo.spongepowered.org/repository/maven-public')
                }
            }
            filter {
                includeGroupAndSubgroups("org.spongepowered")
            }
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = 'Forge'
                    url = uri('https://maven.minecraftforge.net')
                }
            }
            filter {
                includeGroupAndSubgroups('net.minecraftforge')
            }
        }
    }
}

buildscript {
    dependencyLocking {
        lockMode = LockMode.STRICT
        lockAllConfigurations()
        lockFile.set file("locks/settings-buildscript-gradle.lockfile")
    }
}


plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '1.+'
}

// This should match the folder name of the project, or else IDEA may complain (see https://youtrack.jetbrains.com/issue/IDEA-317606)
rootProject.name = 'autoswitch'
include('common')
include('fabric')
include('neoforge')
include('engine')