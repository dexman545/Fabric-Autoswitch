plugins {
    id 'multiloader-common'
    id 'net.fabricmc.fabric-loom-remap'
    id 'jvm-test-suite'
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    compileOnly 'org.spongepowered:mixin:0.8.5'
    // fabric and neoforge both bundle mixinextras, so it is safe to use it in common
    compileOnly 'io.github.llamalad7:mixinextras-common:0.3.5'
    annotationProcessor 'io.github.llamalad7:mixinextras-common:0.3.5'
    api(project(":engine"))
    testImplementation(project(":engine"))
    implementation('com.google.auto.service:auto-service:1.1.1')
    annotationProcessor('com.google.auto.service:auto-service:1.1.1')

    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation "org.assertj:assertj-core:3.27.6"
    testImplementation('com.google.auto.service:auto-service:1.1.1')
    testAnnotationProcessor('com.google.auto.service:auto-service:1.1.1')
}

testing {
    suites {
        test {
            useJUnitJupiter()
            targets {
                all {
                    testTask.configure {
                        filter {
                            includeTestsMatching '*Test'
                            includeTestsMatching '*Tests'
                            includeTestsMatching '*TestCase'
                            excludeTestsMatching 'dex.autoswitch.mixin.*'
                        }

                        delete(project.layout.buildDirectory.dir("unitTests"))
                        mkdir project.layout.buildDirectory.dir("unitTests")
                        workingDir = project.layout.buildDirectory.dir("unitTests")
                    }
                }
            }
        }
    }
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}

